import org.evento.cli.PublishBundle
import org.evento.cli.UpdateVersion

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.eventoframework:evento-cli:ev1.2.2'
    }
}


plugins {
    id 'java'
    id 'java-library'
}

group 'org.evento'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}


dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'


    api(project(':evento-bundle'))
    api(project(':evento-common'))
    api(project(':evento-bus:evento-bus-rabbitmq'))
    api 'io.sentry:sentry:6.23.0'
}

test {
    useJUnitPlatform()
}

tasks.register('eventoUpdateVersion') {
    dependsOn 'clean'
    doLast {
        UpdateVersion.run(file("./src").toString())
    }
}
tasks.register('eventoPublish') {
    dependsOn 'jar'
    doLast {
        PublishBundle.run(file(".").toString(), 'http://localhost:3000',
                'https://github.com/EventoFramework/evento-framework/blob/main/evento-demo/evento-demo-api')
    }
}
tasks.register('eventoUpdateVersionAndPublish') {
    dependsOn 'eventoUpdateVersion'
    finalizedBy(eventoPublish)
}
sourceCompatibility = '19'
targetCompatibility = '19'
